plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.gatling.gradle' version '3.13.5.1'
}

group = 'br.com.postechfiap'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.modelmapper:modelmapper:3.2.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'
    implementation 'io.rest-assured:json-schema-validator:5.5.1'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2' // h2 em runtime
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.h2database:h2'// h2 em testes
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'io.cucumber:cucumber-java:7.21.1'
    testImplementation 'io.cucumber:cucumber-spring:7.21.1'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.21.1'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.12.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    implementation 'io.gatling:gatling-app:3.13.5'
    implementation 'io.gatling.highcharts:gatling-charts-highcharts:3.13.5'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    include '**/*IT.class'  // Apenas testes de integração
    testLogging {
        // Exibe informações sobre os testes passados, falhados e ignorados
        events "PASSED", "FAILED", "SKIPPED"
    }
}

tasks.register('unitTest', Test) {
    useJUnitPlatform()
    include '**/*Test.class'  // Apenas testes unitários
    testLogging {
        // Exibe informações sobre os testes passados, falhados e ignorados
        events "PASSED", "FAILED", "SKIPPED"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        // Exibe informações sobre os testes passados, falhados e ignorados
        events "PASSED", "FAILED", "SKIPPED"
    }
}

tasks.register('systemTest', Test) {
    description = 'Executa apenas os testes do Cucumber via JUnit Platform'
    group = 'verification'
    useJUnitPlatform()

    include '**/CucumberTest.class'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = true
    }
}

tasks.register('performanceTest', GradleBuild) {
    description = 'Executa os testes de performance com Gatling'
    group = 'verification'

    tasks = ['gatlingRun']

    doFirst {
        println 'Iniciando testes de performance com Gatling...'
    }

    doLast {
        println 'Testes de performance concluídos.'
        def reportDir = file("$buildDir/reports/gatling")
        println "Verifique o relatório em: ${reportDir}"
    }
}
